!scriptcards {{ 
  --#title|Chain Lightning
  --#sourceToken|@{selected|token_id}
  --#emoteText|@{selected|character_name} Causes a Cascade of Lightning!
  --&TFColor|#FFFFFF
  --&STFColor|#000000
  --&FColor1|#000000
  --&FColor2|#000000
  --&EBColor|#7DEDFF
  --&BColor1|#7C83FD
  --&BColor2|#96BAFF
  --#titlecardbackground|[&BColor1]
  --#titlecardbackgroundimage|linear-gradient(to bottom, #96BAFF, #7C83FD)
  --#titleFontColor|[&TFColor]
  --#subtitleFontColor|[&STFColor]
  --#emoteBackground|[&EBColor]
  --#titlefontsize|2.0em
  --#titlefontlineheight|1.2em
  --#titlefontface|Times New Roman
  --#norollhighlight|0
  --#hideTitleCard|0
  --#bodyfontsize|12px
  --#bodyfontface|Algerian
  --#oddrowbackground|[&BColor1]
  --#oddrowfontcolor|[&FColor2]
  --#evenrowbackground|[&BColor2]
  --#evenrowfontcolor|[&FColor1]
  --#leftsub|Level 6 | Evocation | Action
  --#rightsub| 150' (30') | V S M

  --#debug|1

  --:Setup|
    -->GetAndCheckSlotInformation|
    --&DamageType|lightning
    --=DC|[*S:spell_save_dc] 

  --=MissileCount|[$SlotLevel] - 2
  -->BuildAndAskTargets|[$MissileCount.Total]
  --=DisplayCount|1
  --=MissileDamage|10d8
  --+|[c]Spell Save DC: [$DC]  Damage: [#ffff00][$MissileDamage][/#][/c]

  --:MissileLoop|
  -->FireMissile|[$DisplayCount.Total]
  --=DisplayCount|[$DisplayCount] + 1
  --?[$DisplayCount] -le [$MissileCount]|MissileLoop

  --X|

  --:FireMissile|
  --&ThisTarget|[&target[%1%]]
  -->PlayEffects|@{selected|token_id};[&ThisTarget];none;burst-smoke;beam-magic;spell_01
  -->CheckSave|[&ThisTarget]
  --<|

  --:PlayEffects|Parameters are : sourcetoken; targettoken; source effect; target effect; line effect; sound effect
  --vtoken|[%1%] [%3%]
  --vtoken|[%2%] [%4%]
  --vbetweentokens|[%1%] [%2%] [%5%]
  --@roll20AM|_audio,play,nomenu|[%6%]
  --<|

  --:BuildAndAskTargets|
  --&TargetString|
  --=targetCount|1
  --:TargetLoop|
  --&TargetString|+t;target[$targetCount.Raw];Missile [$targetCount.Raw] Target
  --=targetCount|[$targetCount.Total] + 1
  --?[$targetCount.Total] -le [%1%]|>AddSeparator
  --?[$targetCount.Total] -le [%1%]|TargetLoop
  --iClick button below to select targets. The same target MAY NOT be selected more than once;Select [%1%] Targets|[&TargetString]
  --<|

  --:AddSeparator|
  --&TargetString|+||
  --<|

  --:EndMacro

  --X|

  --:PROCEDURES|

  --:GetAndCheckSlotInformation|
     --=SlotLevel|?{Spell Slot Level?|6|7|8|9}
     --=SlotsTotal|[*S:lvl[$SlotLevel]_slots_total]
     --=SlotsExpended|[*S:lvl[$SlotLevel]_slots_expended]
     --?[$SlotsExpended.Total] -ge [$SlotsTotal.Total]|NoSlotsLeft

  --:DeductSpellSlot|
     --=SlotsExpended|[$SlotsExpended] + 1
     --@setattr|_charid [*S:character_id] _lvl[$SlotLevel]_slots_expended|[$SlotsExpended] _silent
     --=SlotsRemaining|[$SlotsTotal] - [$SlotsExpended]
  --<|

  --:NoSlotsLeft|
     --+|[*S:character_name] has no level [$SlotLevel.Total] spell slots available.
  --X|

  --:CheckSave|pass ThisTarget; Damage
     --=TotalDamage|[$MissileDamage]
     --?"[*[%1%]:npc_immunities]" -inc "[&DamageType]"|>Immune;[%1%]
     --?"[*[%1%]:npc_resistances]" -inc "[&DamageType]"|>Resistant;[%1%];[$TotalDamage]
     --?"[*[%1%]:npc_vulnerabilities]" -inc "[&DamageType]"|>Vulnerable;[%1%];[$TotalDamage]

     --:Attribute Assign|
        --=AttributeBase|[*[%1%]:npc_dex_save_base]
        --?[$AttributeBase.Total] -gt 0|SaveRoll
       --=AttributeBase|[*[%1%]:dexterity_mod]

       --:SaveRoll|
          --=SaveRoll|1d20 [SAVE] + [$AttributeBase.Total] [MOD]
          --?[$SaveRoll] -ge [*S:spell_save_dc]|>Save;[%1%];[$TotalDamage]|>Fail;[%1%];[$TotalDamage]
  --<|

  --:Fail|
     --+|[b][*[%1%]:t-name] Fails!| Roll: [$SaveRoll][r]Damage: [$TotalDamage][/r][/b]
     -->ApplyDamageAlterbars|[%1%];1;-[$TotalDamage]
  --<|

  --:Save|
     --=HalfDamage|[$TotalDamage] \ 2
     --+|[b][*[%1%]:t-name] Saves!| Roll: [$SaveRoll][r]Damage: [$HalfDamage][/r][/b]
     -->ApplyDamageAlterbars|[%1%];1;-[$HalfDamage]
  --<|

  --:Resistant|
     --+|[b][*[%1%]:t-name] is Resistant to [&DamageType] damage and takes half damage![/b]
     --=TotalDamage|[$TotalDamage] \ 2
  --<|

  --:Vulnerable|
     --+|[b][*[%1%]:t-name] is Vulnerable to [&DamageType] damage and takes double damage![/b]
     --=TotalDamage|[$TotalDamage] * 2
  --<|

  --:Immune|
     --+[b]Immune![/b]|[b][*[[%1%]]:t-name]:|is not effected by the spell![/b]
     --=DisplayCount|[$DisplayCount] + 1

  --^MissileLoop|

  --:ApplyDamageAlterbars|
     --@alter|_target|[%1%] _bar|[%2%] _amount|[%3%]
  --<|

}}